
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Компонента", Компонента) Тогда
		ЭтаФорма.Заголовок = СтрШаблон(
			НСтр("ru = 'Просмотр логов (%1)'"),
				Компонента);
		
		Параметры.Свойство("ИмяФайла", ЛогИмяФайла);
	КонецЕсли;
	
	ПолучитьЛогиКомпоненты();	
	
	ГруппироватьСообщения = Ложь;
		
	ОтборПоПериоду.ДатаНачала = НачалоДня(ТекущаяДатаСеанса());
	ОтборПоПериоду.ДатаОкончания = КонецДня(ТекущаяДатаСеанса()) + 1;
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Получить(Команда)
		
	Если ЗначениеЗаполнено(ЛогИмяФайла) Тогда
		ПолучитьСообщения(ЛогИмяФайла);
	Иначе
		ТекущиеДанные = Элементы.Логи.ТекущиеДанные;
		Если ТекущиеДанные <> Неопределено Тогда
			ПолучитьСообщения(ТекущиеДанные.ИмяФайла);		
		Иначе
			ПоказатьПредупреждение(, НСтр("ru = 'Для получения сообщений необходимо выбрать Компоненту и Лог'"));		
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКНачалу(Команда)
	
	Если КоличествоСообщений > 0 Тогда	
		ТекущаяСтраница = Элементы.ТекущаяСтраница.СписокВыбора.Получить(0).Значение;
		
		ПерейтиКСтраницеНаСервере();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиНазад(Команда)
	
	Если КоличествоСообщений > 0 Тогда
		Если ТекущаяСтраница > 1 Тогда
			ТекущаяСтраница = ТекущаяСтраница - 1;
			ПерейтиКСтраницеНаСервере();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиВперед(Команда)
	
	Если КоличествоСообщений > 0 Тогда
		Если ТекущаяСтраница < Элементы.ТекущаяСтраница.СписокВыбора.Количество() Тогда
			ТекущаяСтраница = ТекущаяСтраница + 1;
			ПерейтиКСтраницеНаСервере();
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиККонцу(Команда)
	
	Если КоличествоСообщений > 0 Тогда
		ТекущаяСтраница = Элементы.ТекущаяСтраница.СписокВыбора.Получить(
			Элементы.ТекущаяСтраница.СписокВыбора.Количество() - 1).Значение;
			
		ПерейтиКСтраницеНаСервере();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура КомпонентаПриИзменении(Элемент)
	
	ПолучитьЛогиКомпоненты();
	
КонецПроцедуры

&НаКлиенте
Процедура ГруппироватьПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);	
	
	СформироватьПредставление();
	
КонецПроцедуры

&НаКлиенте
Процедура СообщенияОтображениеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.СообщенияОтображение.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда	
		ЛоггерКлиент.ПоказатьСообщение(
			ТекущиеДанные.Период,
			ТекущиеДанные.Уровень,
			ТекущиеДанные.Объект,
			ТекущиеДанные.Текст
		);
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ТекущаяСтраницаПриИзменении(Элемент)
	
	ПерейтиКСтраницеНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ЛогиПриАктивизацииСтроки(Элемент)

	ТекущиеДанные = Элементы.Логи.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ЛогИмяФайла = ТекущиеДанные.ИмяФайла;
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ПолучитьЛогиКомпоненты()
	
	Логи.Параметры.УстановитьЗначениеПараметра("Ссылка", Компонента);
	
	Элементы.Логи.Обновить();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(ЭтаФорма)
	
	Элементы = ЭтаФорма.Элементы;
	
	Элементы.СообщенияОтображениеКоличество.Видимость = ЭтаФорма.ГруппироватьСообщения;
	Элементы.СообщенияОтображениеПериод.Видимость = Не ЭтаФорма.ГруппироватьСообщения;
	
	Элементы.ГруппаЛоги.Видимость = Не ЗначениеЗаполнено(ЭтаФорма.ЛогИмяФайла);
	
КонецПроцедуры

&НаСервере
Процедура ПолучитьСообщения(ИмяФайла, Страница = 1)
	
	Сообщения.Очистить();
	
	Смещение = (Страница - 1) * 100;
	
	СообщенияЛога = Новый Массив();
	Если ВыбранОтборПоПериоду() Тогда
		СообщенияЛога = Компоненты.ПоискПоПериоду(Компонента, ИмяФайла, 
			ОтборПоПериоду.ДатаНачала, 
			ОтборПоПериоду.ДатаОкончания,
			,
			Смещение);	
		
	ИначеЕсли ВыбранОтборПоТексту() Тогда
		Если ПустаяСтрока(ОтборПоТексту) Тогда
			Сообщить(НСтр("ru = 'Не заполнено значение поиска'"));
			Возврат;
		КонецЕсли;
				
		СообщенияЛога = Компоненты.ПоискПоТексту(Компонента, ИмяФайла,
			ОтборПоТексту,
			,
			Смещение);		
	КонецЕсли;

	КоличествоСообщений = СообщенияЛога.Количество;
	
	Для Каждого ЭлементКоллекции Из СообщенияЛога.Сообщения Цикл
		НоваяСтрока = Сообщения.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, ЭлементКоллекции);
		НоваяСтрока.Количество = 1;		
	КонецЦикла;
	
	// Постраничный просмотр
	КоличествоСтраниц = ?(КоличествоСообщений % 100 > 0, 
		Цел(КоличествоСообщений / 100) + 1,
		Цел(КоличествоСообщений / 100));
		
	Элементы.ТекущаяСтраница.СписокВыбора.Очистить();
	Для Индекс = 1 По КоличествоСтраниц Цикл
		Элементы.ТекущаяСтраница.СписокВыбора.Добавить(Индекс);		
	КонецЦикла;	
	
	ТекущаяСтраница = Страница;
	
	СформироватьПредставление();
		
КонецПроцедуры

&НаСервере
Процедура СформироватьПредставление()
	
	СообщенияОтображение.Очистить();
	
		// Представление
	//Если ГруппироватьСообщения Тогда
	//	Свертываемая = Новый ТаблицаЗначений();
	//	Свертываемая.Колонки.Добавить("Объект", Новый ОписаниеТипов("Строка"));
	//	Свертываемая.Колонки.Добавить("Уровень", Новый ОписаниеТипов("Строка"));
	//	Свертываемая.Колонки.Добавить("Текст", Новый ОписаниеТипов("Строка"));
	//	Свертываемая.Колонки.Добавить("Количество", Новый ОписаниеТипов("Число"));
	//	Для Каждого ЭлементКоллекции Из Сообщения Цикл
	//		ЗаполнитьЗначенияСвойств(Свертываемая.Добавить(), ЭлементКоллекции);
	//	КонецЦикла;		
	//	Свертываемая.Свернуть("Объект, Уровень, Текст", "Количество");
	//	
	//	Для Каждого ЭлементКоллекции Из Свертываемая Цикл
	//		ЗаполнитьЗначенияСвойств(СообщенияОтображение.Добавить(), ЭлементКоллекции);
	//	КонецЦикла;
	//Иначе		
		Для Каждого ЭлементКоллекции Из Сообщения Цикл
			ЗаполнитьЗначенияСвойств(СообщенияОтображение.Добавить(), ЭлементКоллекции);
		КонецЦикла;
	//КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПерейтиКСтраницеНаСервере()
		
	ПолучитьСообщения(ЛогИмяФайла, ТекущаяСтраница);	
	
КонецПроцедуры

&НаСервере
Функция ВыбранОтборПоПериоду()
	
	Возврат Элементы.СтраницыОтборы.ТекущаяСтраница = Элементы.СтраницаОтборПоПериоду;
	
КонецФункции

&НаСервере
Функция ВыбранОтборПоТексту()
	
	Возврат Элементы.СтраницыОтборы.ТекущаяСтраница = Элементы.СтраницаПоТексту;
	
КонецФункции

#КонецОбласти