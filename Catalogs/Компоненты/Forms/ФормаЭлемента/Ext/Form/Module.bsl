
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой(ЭтаФорма);	
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если ТекущийОбъект.Использование Тогда
		Версия = Компоненты.ВерсияКомпоненты(ТекущийОбъект.Ссылка);
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ИспользованиеПриИзменении(Элемент)
	
	Если Объект.Использование Тогда
		Версия = КомпонентыВызовСервера.ВерсияКомпоненты(
			Новый Структура("Хост, Порт", Объект.Хост, Объект.Порт));
	Иначе
		Версия = Неопределено;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОчиститьЛог(Команда)
	
	ТекущиеДанные = Элементы.Логи.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ПоказатьВопрос(
			Новый ОписаниеОповещения("ОчиститьЛог_ПослеВыбораОтвета", ЭтотОбъект, 
				Новый Структура("Компонента, Лог", Объект.Ссылка, ТекущиеДанные.ИмяФайла)),
			СтрШаблон(
				НСтр("ru = 'Будет выполнено удаление содержимого лога (%1). Продолжить?'"),
					ТекущиеДанные.ИмяФайла),
			РежимДиалогаВопрос.ДаНет);
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Для удаления содержимого необходимо выбрать нужный лог'"));	
	КонецЕсли;
			
КонецПроцедуры

&НаКлиенте
Процедура ПросмотретьЛог(Команда)
	
	ТекущиеДанные = Элементы.Логи.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		ОткрытьФорму("ОбщаяФорма.ПросмотрЛогов", 
			Новый Структура("Компонента, ИмяФайла", Объект.Ссылка, ТекущиеДанные.ИмяФайла),
			,
			Истина,
			,
			,
			,
			РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);		
	Иначе
		ПоказатьПредупреждение(, НСтр("ru = 'Для просмотра содержимого необходимо выбрать нужный лог'"));	
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДоступныеЛоги(Команда)

	ОткрытьФорму("Справочник.Компоненты.Форма.ДоступныеЛоги",
		Новый Структура("Компонента", Объект.Ссылка),
		,
		,
		,
		,
		Новый ОписаниеОповещения("ДоступныеЛоги_ПослеЗакрытия", ЭтаФорма),
		РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ДоступныеЛоги_ПослеЗакрытия(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено Тогда
		Для Каждого ИмяФайла Из Результат Цикл
			Найденное = Объект.Логи.НайтиСтроки(
				Новый Структура("ИмяФайла", ИмяФайла));
				
			Если Найденное.Количество() = 0 Тогда
				НоваяСтрока = Объект.Логи.Добавить();
				НоваяСтрока.Активность = Истина;
				НоваяСтрока.ИмяФайла = ИмяФайла;
				
				Файл = Новый Файл(ИмяФайла);
				НоваяСтрока.Представление = Файл.ИмяБезРасширения;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры


&НаКлиенте
Процедура ОчиститьЛог_ПослеВыбораОтвета(Результат, ДополнительныеПараметры) Экспорт
	
	ОписаниеОшибки = Неопределено;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ТекстОшибки = Неопределено;
		Результат = ОчиститьЛогНаСервере(ДополнительныеПараметры.Компонента,
			ДополнительныеПараметры.Лог,
			ТекстОшибки);
			
		Если Результат Тогда
			ПоказатьПредупреждение(, НСтр("ru = 'Операция выполнена успешно'"));
		Иначе
			ПоказатьПредупреждение(, 
				СтрШаблон(
					НСтр("ru = 'В процессе удаления содержимого возникла ошибка: %1'"), ТекстОшибки));
		КонецЕсли;
	КонецЕсли;
			
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(ЭтаФорма)
	
КонецПроцедуры

&НаСервере
Функция ОчиститьЛогНаСервере(Компонента, Лог, ОписаниеОшибки) 
	
	Результат = Ложь;
	ОписаниеОшибки = Неопределено;
	
	Попытка
		Компоненты.ОчиститьВсеСообщения(Компонента, Лог);			
		Результат = Истина;
	Исключение
		ОписаниеОшибки = ОписаниеОшибки();
	КонецПопытки;
	
	Возврат Результат;
	
КонецФункции

#КонецОбласти