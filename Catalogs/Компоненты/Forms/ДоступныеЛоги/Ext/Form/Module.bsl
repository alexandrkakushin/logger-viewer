
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ПараметрКомпонента = Неопределено;
	Если Не Параметры.Свойство("Компонента", Компонента) Тогда
		Сообщить(НСтр("ru = 'Отсутствует обязательный параметр <Компонента> при открытии формы'"));
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(Компонента) Тогда
		Сообщить(НСтр("ru = 'Не выбрана компонента для отображения логов'"));
		Возврат;
	КонецЕсли;
	
	ОбновитьСписокНаСервере();
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Выбрать(Команда)
	
	Выбранные = Новый Массив();	
	Для Каждого ЭлементКоллекции Из Логи Цикл
		Если ЭлементКоллекции.Пометка Тогда
			Выбранные.Добавить(ЭлементКоллекции.ИмяФайла);
		КонецЕсли;
	КонецЦикла;
	
	Закрыть(Выбранные);		
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСписок(Команда)

	ОбновитьСписокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	ОбработатьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	ОбработатьФлажки(Истина);
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьФлажки(Пометка)
	
	Для Каждого ЭлементКоллекции Из Логи Цикл
		ЭлементКоллекции.Пометка = Пометка;
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьСписокНаСервере()
	
	Логи.Очистить();
	
	ЛогиКомпоненты = Компоненты.ЛогиКомпоненты(Компонента);
	Для Каждого Лог Из ЛогиКомпоненты Цикл
		НоваяСтрока = Логи.Добавить();
		НоваяСтрока.ИмяФайла = Лог;
	КонецЦикла;	
	
	Логи.Сортировать("ИмяФайла ВОЗР");
		
КонецПроцедуры

#КонецОбласти