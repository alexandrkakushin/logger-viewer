
&НаКлиенте
Процедура ОбработкаКоманды(ПараметрКоманды, ПараметрыВыполненияКоманды)
	
	ПоказатьВопрос(
		Новый ОписаниеОповещения(
			"ПослеВыбораОтвета", ЭтотОбъект, Новый Структура("Компонента", ПараметрКоманды)),
		НСтр("ru = 'Будет выполнено удаление содержимого логов компоненты. Продолжить?'"), 
		РежимДиалогаВопрос.ДаНет);
		
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораОтвета(Результат, ДополнительныеПараметры) Экспорт
	
	ОписаниеОшибки = Неопределено;
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ТекстОшибки = Неопределено;

		Результат = ОчиститьВсеЛоги(ДополнительныеПараметры.Компонента);
		
		ОткрытьФорму("Справочник.Компоненты.Форма.РезультатОчисткиЛогов",
			Новый Структура("Компонента, Результат",
				ДополнительныеПараметры.Компонента,
				Результат));		
	КонецЕсли;
			
КонецПроцедуры


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

&НаСервере
Функция ОчиститьВсеЛоги(Компонента) 
	
	Логи = Новый Массив();
	
	Для Каждого Лог Из Компоненты.ЛогиКомпоненты(Компонента) Цикл
		Результат = Ложь;
		ОписаниеОшибки = Неопределено;
		
		Попытка
			Компоненты.ОчиститьВсеСообщения(Компонента, Лог, ОписаниеОшибки);			
			Результат = Истина;
		Исключение
			ОписаниеОшибки = ОписаниеОшибки();
		КонецПопытки;
		
		Логи.Добавить(
			Новый Структура("Лог, Результат, ОписаниеОшибки",
				Лог,
				Результат,
				ОписаниеОшибки));
	КонецЦикла;	
	
	Возврат Логи;
		
КонецФункции

#КонецОбласти