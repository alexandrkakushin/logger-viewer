
///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Обновить(Команда)
	
	ОбновитьНаСервере();
	
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ПоследниеЧасВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	ТекущиеДанные = Элементы.ПоследниеЧас.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда	
		ЛоггерКлиент.ПоказатьСообщение(
			ТекущиеДанные.Период,
			ТекущиеДанные.Уровень,
			ТекущиеДанные.Объект,
			ТекущиеДанные.Текст
		);
	КонецЕсли;
		
КонецПроцедуры

#КонецОбласти


///////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ОбновитьНаСервере()
	
	ПоследниеЧас.Очистить();
	
	Запрос = Новый Запрос();
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	КомпонентыЛоги.Ссылка КАК Ссылка,
		|	КомпонентыЛоги.ИмяФайла КАК ИмяФайла
		|ИЗ
		|	Справочник.Компоненты.Логи КАК КомпонентыЛоги
		|ГДЕ
		|	КомпонентыЛоги.Активность";
	
	РезультатЗапроса = Запрос.Выполнить();
	Если Не РезультатЗапроса.Пустой() Тогда
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл						
			Попытка
				Сообщения = Компоненты.Сообщения(Выборка.Ссылка, Выборка.ИмяФайла, ТекущаяДатаСеанса() - 3600, ТекущаяДатаСеанса());							
				Для Каждого Сообщение Из Сообщения Цикл
					НоваяСтрока = ПоследниеЧас.Добавить();
					НоваяСтрока.Компонента = Выборка.Ссылка;
					НоваяСтрока.Лог        = Выборка.ИмяФайла;							
					ЗаполнитьЗначенияСвойств(НоваяСтрока, Сообщение);
				КонецЦикла;
			Исключение
				Сообщить(ОписаниеОшибки());
			КонецПопытки;			
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти